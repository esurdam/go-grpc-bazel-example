load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_embed_data", "go_library")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push", "oci_tarball")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")

go_embed_data(
    name = "static_assets",
    src = "//pb/helloworld:helloworld_openapi_swagger",
    package = "main",
)

go_library(
    name = "helloworld_lib",
    srcs = ["main.go"],
    embed = [":static_assets"],
    importpath = "github.com/AdGreetz/go-grpc-bazel-example/services/helloworld",
    visibility = ["//visibility:private"],
    deps = [
        "//pb/helloworld",
        "//pkg/helloworld/server",
        "@com_github_grpc_ecosystem_grpc_gateway_v2//runtime",
        "@com_github_philip_bui_grpc_zerolog//:grpc-zerolog",
        "@org_golang_google_grpc//:go_default_library",
        "@org_golang_google_grpc//credentials",
    ],
)

go_binary(
    name = "helloworld",
    embed = [":helloworld_lib"],
    visibility = ["//visibility:public"],
)

# Put app go_binary into a tar layer.
pkg_tar(
    name = "app_layer",
    srcs = [":helloworld"],
)

oci_image(
    name = "image",
    base = "@distroless_base",
    entrypoint = ["/helloworld"],
    tars = [":app_layer"],
)

platform_transition_filegroup(
    name = "transitioned_image",
    srcs = [":image"],
    target_platform = select({
        "@platforms//cpu:arm64": "@io_bazel_rules_go//go/toolchain:linux_arm64",
        "@platforms//cpu:x86_64": "@io_bazel_rules_go//go/toolchain:linux_amd64",
    }),
)

# $ bazel run :tarball
# $ docker run --rm gcr.io/example:latest
oci_tarball(
    name = "tarball",
    # Use the image built for the exec platform rather than the target platform
    image = ":image",
    repo_tags = ["ghcr.io/adgreetz/go-grpc-bazel-example/services/helloworld:latest"],
)

write_file(
    name = "tags_tmpl",
    out = "tags.txt.tmpl",
    content = [
        "BUILD_VERSION",
    ],
)

expand_template(
    name = "stamped",
    out = "_stamped.tags.txt",
    stamp_substitutions = {"BUILD_VERSION": "{{BUILD_EMBED_LABEL}}"},
    substitutions = {"BUILD_VERSION": "0.0.0"},
    template = "tags_tmpl",
)

oci_push(
    name = "push",
    image = ":image",
    remote_tags = ":stamped",
    repository = "ghcr.io/adgreetz/go-grpc-bazel-example/services/helloworld",
)

#k8s_deploy(
#    name = "k8s",
#    images = {
#        "services/helloworld:latest": "//services/helloworld:docker",
#    },
#    template = "//ci/services:helloworld.yaml",
#)
